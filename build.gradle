plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

group 'io.github.math0898'
version '3.0.0-zeta5'
var isSnapshot = true

repositories {
    mavenCentral()
}

compileJava {
    options.release = 24
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId(group)
            if (isSnapshot)
                artifactId('suga-engine-snapshot')
            else
                artifactId('suga-engine')
            version(version) // Pulls version from Gradle root
            from components.java

            pom {
                name = 'Sugaku Engine'
                description = 'The Suga engine is meant to be a fully functional game engine written in Java without extra dependencies.'
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/math0898/SugaEngine"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

check {
    finalizedBy(jacocoTestReport)
}
